group 'ssyp.ws02'
version '1.0'


buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.3"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

//noinspection GroovyUnusedAssignment
mainClassName = 'MainKt'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.7

task wrapper(type: Wrapper) {
    gradleVersion = "2.14"
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

// create a single Jar for CLI with all dependencies
task fatJar(type: Jar) {
    manifest {
        //noinspection GroovyAssignabilityCheck
        attributes(
                "Implementation-Title": "PETOOH Khan",
                "Implementation-Version": "1.0",
                "Main-Class": "MainKt"
        )
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    //noinspection GroovyAssignabilityCheck
    with jar
}

// create a single Jar for GUI with all dependencies
task fatJarGui(type: Jar) {
    manifest {
        //noinspection GroovyAssignabilityCheck
        attributes(
                "Implementation-Title": "PETOOH Khan GUI",
                "Implementation-Version": "1.0",
                "Main-Class": "gui.GUIApp"
        )
    }
    baseName = project.name + "-gui"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    //noinspection GroovyAssignabilityCheck
    with jar
}

defaultTasks 'test', 'fatJar', 'fatJarGui'

repositories {
    mavenCentral()
}

dependencies {
    compile 'no.tornado:tornadofx:1.5.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.3"
    compile "org.ow2.asm:asm-all:5.0.1"
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.jetbrains.kotlin:kotlin-test:1.0.3"
}
